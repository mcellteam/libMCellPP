begin model
begin parameters
	V = 0.125*1e-15 # um^3 -> liters
	NA = 6.022e23/1e6
	#Rate Constants (The numbers used here are taken from the SBML file available online)
	k_on1C = 4/(NA*V) #1/uM 1/s 
	k_off1C = 40 #1/s (10-70)
	k_on2C = 10/(NA*V) #1/uM 1/s 
	k_off2C = 9.25 #1/s (8.5-10)	
	k_on1N = 100/(NA*V) #1/uM 1/s 
	k_off1N = 2500 #1/s
	k_on2N = 150/(NA*V) #1/uM 1/s 
	k_off2N = 750 #1/s 
	
	k_onCaM1C1N = 3.3/(NA*V) #1/uM 1/s 
	k_offCaM1C1N = 3.4 #1/s
	
	k_onCaMKII = 50/(NA*V) #1/uM 1/s 
	k_offCaMKII = 60 #1/s 
	
	k_pCaM1C1N = 0.094 #1/s 
	
end parameters

begin molecule types
	Ca()
	CaM(C~0~1~2,N~0~1~2,ng,camkii)
	Ng(cam)
	CaMKII(d,r,l,Y286~0~P,S306~0~P,cam)
end molecule types
begin seed species
	Ca() 10*(NA*V) #uM 
	CaM(C~0,N~0,ng,camkii) 30*(NA*V) #uM
	CaMKII(d,r,l,Y286~0,S306~0,cam) 80*(NA*V) #uM
end seed species

begin observables
	Molecules Ca Ca()
	Molecules CaM CaM()
	Molecules CaM1N CaM(C~0,N~1,camkii)
	Molecules CaM2N CaM(C~0,N~2,camkii)
	Molecules CaM1C CaM(C~1,N~0,camkii)
	Molecules CaM1C1N CaM(C~1,N~1,camkii)
	Molecules CaM1C2N CaM(C~1,N~2,camkii)
	Molecules CaM2C CaM(C~2,N~0,camkii)
	Molecules Cam2C1N CaM(C~2,N~1,camkii)	
	Molecules Cam4Ca CaM(C~2,N~2,camkii)

	Molecules KCaM1C1N CaMKII(Y286~0,cam!1).CaM(C~1,N~1,camkii!1)
	Molecules pKCaM1C1N CaMKII(Y286~P,cam!1).CaM(C~1,N~1,camkii!1)

	## Counting can also be done with don't care don't write
	Molecules pKCaMII CaMKII(Y286~P,cam)
	Molecules pKCaM_tot CaMKII(Y286~P)
	Molecules KCaMKII CaMKII(Y286~0)

	
end observables

begin reaction rules
	#rxn 1-4
	CaM(C~0,ng,camkii) + Ca <-> CaM(C~1,ng,camkii) k_on1C,k_off1C
	CaM(C~1,ng,camkii) + Ca <-> CaM(C~2,ng,camkii) k_on2C,k_off2C
	CaM(N~0,ng,camkii) + Ca <-> CaM(N~1,ng,camkii) k_on1N,k_off1N
	CaM(N~1,ng,camkii) + Ca <-> CaM(N~2,ng,camkii) k_on2N,k_off2N

	# rxn 5
	CaMKII(l,r,Y286~0,cam) + CaM(C~1,N~1,camkii) <-> CaMKII(l,r,Y286~0,cam!1).CaM(C~1,N~1,camkii!1) k_onCaM1C1N,k_offCaM1C1N

	#rxn 6
	CaMKII(l,r,Y286~0,cam!+) + CaMKII(l,r,cam!+) <-> CaMKII(l!1,r,Y286~0,cam!+).CaMKII(l,r!1,cam!+) k_onCaMKII, k_offCaMKII

	#rxn 7
	CaMKII(l!1,Y286~0,cam!2).CaM(C~1,N~1,camkii!2).CaMKII(r!1,cam!+) -> CaMKII(l,Y286~P,cam!1).CaM(C~1,N~1,camkii!1) + CaMKII(r,cam!+) k_pCaM1C1N


end reaction rules
end model

simulate({method=>"nf",t_end =>1500,n_steps => 1000})